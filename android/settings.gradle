pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }
    settings.ext.flutterSdkPath = flutterSdkPath()

    includeBuild("${settings.ext.flutterSdkPath}/packages/flutter_tools/gradle")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.1.1" apply false
    id "org.jetbrains.kotlin.android" version "1.9.22" apply false
}

include ":app"

gradle.beforeProject { project ->
    project.afterEvaluate {
        if (it.hasProperty('android') && it.android.hasProperty('namespace')) {
            def android = it.android
            if (android.namespace == null || android.namespace.isEmpty()) {
                // Get package from AndroidManifest.xml
                def manifestFile = file("${it.projectDir}/src/main/AndroidManifest.xml")
                if (manifestFile.exists()) {
                    def manifest = new XmlSlurper().parse(manifestFile)
                    def packageName = manifest.@package.toString()
                    if (packageName) {
                        android.namespace = packageName
                    }
                }
            }
        }
    }
}
